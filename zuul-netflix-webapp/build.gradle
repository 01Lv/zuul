apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'jetty'

configurations {
    all*.exclude group: 'asm', module: 'asm'
    all*.exclude group: 'asm', module: 'asm-all'
}

dependencies {

    compile project(':zuul-core')
    compile project(':zuul-servlet-api')

    compile 'com.netflix.karyon:karyon-core:2.0.02'
    compile 'com.netflix.karyon:karyon-admin:2.0.02'
    compile 'com.netflix.karyon:karyon-admin-web:2.0.02'
    compile 'com.netflix.karyon:karyon-extensions:2.0.02'
    compile 'com.netflix.blitz4j:blitz4j:1.35'
    compile 'com.sun.jersey:jersey-core:1.19'
    compile 'com.sun.jersey:jersey-server:1.19'
    compile 'com.sun.jersey:jersey-client:1.19'
    compile 'com.sun.jersey:jersey-servlet:1.19'

    runtime 'org.ow2.asm:asm:5.0.3'
    runtime 'org.ow2.asm:asm-all:5.0.3'
    runtime 'org.apache.xbean:xbean-bundleutils:3.17'
    runtime 'org.apache.xbean:xbean-finder:3.17'

    compile "org.codehaus.groovy:groovy-all:2.3.6"
    providedCompile 'junit:junit-dep:4.10'
}

sourceSets {
    test {
        java {
            srcDirs  = ['src/main/java']
            include '**/*.java'
        }
        groovy {
            srcDirs  = ['src/main/groovy']
            include '**/*.groovy'
        }
    }
}

javadoc {
    options {
        doclet = "org.benjchristensen.doclet.DocletExclude"
        docletpath = [rootProject.file('./gradle/doclet-exclude.jar')]
        stylesheetFile = rootProject.file('./gradle/javadocStyleSheet.css')
        windowTitle = "Zuul Javadoc ${project.version}"
    }
}

eclipse {
    classpath {
        downloadSources = true
        downloadJavadoc = true
    }
}

war {
    webXml = file('src/main/webapp/WEB-INF/web.xml')
    webInf{
        from('src/main/groovy/filters') {
            into('classes')
        }
    }
}

jettyRun.contextPath = '/'

jettyRun.doFirst {
    System.setProperty("archaius.deployment.applicationId", "zuul")
    System.setProperty("archaius.deployment.stack", "local")
    System.setProperty("archaius.deployment.region", "us-east-1")
    System.setProperty("archaius.deployment.environment", "test")
    System.setProperty("zuul.filters.root", "/Users/michaels/Projects/oss-zuul-1/zuul-netflix-webapp/src/main/groovy/filters")
}

task integrationTest(type: Test) {
    include '**/*IntegrationTest*'
    doFirst {
        jettyRun.httpPort = 8080    // Port for test
        jettyRun.daemon = true
        jettyRun.execute()
    }
    doLast {
        jettyStop.stopPort = 8091   // Port for stop signal
        jettyStop.stopKey = 'stopKey'
        jettyStop.execute()
    }
}